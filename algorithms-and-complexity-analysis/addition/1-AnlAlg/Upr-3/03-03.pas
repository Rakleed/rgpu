   { Демонстрация вычисления последовательности, заданной }
   { рекуррентным соотношением первого порядка:           }
   {                                                      }
   {  F(1)=2, F(n)=3*F(n-1), n=2,3,...                    }
   {                                                      }
   { Демонстрация определения времени вычислений          }
   { ---------------------------------------------------- }
   PROGRAM Primer_1_1;
      Uses  DOS, { Библиотека, содержащая процедуру GetTime }
            CRT; { Библиотека, содержащая процедуру ClrScr  }
      const C1= 1;
            C2=15;
      var   H0,M0,S0,s0100,H1,M1,S1,S1100: Word;
            k: LongInt;
  { ------------------------------------- }
   FUNCTION Prim_1_1 (n: Integer): LongInt;
   { Рекурсивное вычисление элемента последо- }
   { вательности с номером n                  }
   { ---------------------------------------- }
   BEGIN
      If n=1
        then Prim_1_1:=3
        else Prim_1_1:=3*Prim_1_1(n-1)
   END;
  { ----------------------------------- }
   FUNCTION Prim_2_3 (n: Integer): Real;
   { Нерекурсивное вычисление элемента последо- }
   { вательности с номером n                    }
   { ------------------------------------------ }
   BEGIN
      Prim_2_3:=Exp(n*Ln(3.0))
   END;
  { ---------------------------------------- }
   FUNCTION WorkTime(Hour0,Min0,Sec0,sec0100,
                     Hour1,Min1,Sec1,Sec1100: Word): Real;
   { Функция вычисляет время, в секундах, от начального }
   { до конечного моментов работы фрагмента программы:  }
   { Hour0 : Min0 : Sec0 : Sec0100 - начальное время    }
   { Hour1 : Min1 : Sec1 : Sec1100 - конечное время     }
   { (часы, минуты, секунды, 1/100 секунды)             }
   { -------------------------------------------------- }
      var OldTime: Real;   { Начальное время в секундах }
          NewTime: Real;   { Конечное время в секундах  }
   BEGIN
      OldTime:=Hour0*60*60+Min0*60+Sec0+Sec0100/100;
      NewTime:=Hour1*60*60+Min1*60+Sec1+Sec1100/100;
      WorkTime:=NewTime-OldTime
   END;
  { --- }
   BEGIN
      ClrScr;
      WriteLn('Рекурсивное вычисление: ');
      For k:=C1 to C2 do
        begin
          GetTime(H0,M0,S0,S0100);
          Write('  Prim_1_1(',k:2,')=',Prim_1_1(k):8,'  ');
          GetTime(H1,M1,S1,S1100);
          WriteLn(WorkTime(H0,M0,S0,S0100,H1,M1,S1,S1100):5:2,' сек');
        end;
      WriteLn;
      WriteLn('Нерекурсивное вычисление: ');
      For k:=C1 to C2 do
        begin
          GetTime(H0,M0,S0,S0100);
          Write('  Prim_2_3(',k:2,')=',Prim_2_3(k):8:0,'  ');
          GetTime(H1,M1,S1,S1100);
          WriteLn(WorkTime(H0,M0,S0,S0100,H1,M1,S1,S1100):5:2,' сек');
        end;
      WriteLn;
      ReadKey
   END.
